buildscript {
    ext.kotlin_version = '1.2.60'
    ext.coroutines_version = '0.23.4'
    ext.dl4j_version = '1.0.0-beta2'
    ext.serialization_version = '0.6.1'
    ext.jackson_version = '2.9.6'

    repositories {
        mavenCentral()
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlinx:kotlinx-gradle-serialization-plugin:$serialization_version"
    }
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'jacoco'
    apply from: '../maven_push.gradle'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven {
            // Maven Central Snapshot Repo
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    dependencies{
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

        //Units of Measurement Library
        compile 'org.tenkiv.physikal:core:2.0.2.0-SNAPSHOT'

        //General utilities for Kotlin
        compile 'org.tenkiv.coral:coral:2.1.0.1-SNAPSHOT'
        compile 'io.arrow-kt:arrow-core:0.7.2'
        implementation "org.jetbrains.kotlin:kotlin-reflect"

        //Kotlin Coroutines
        compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$coroutines_version"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-nio:$coroutines_version"

        //Testing Framework
        testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.1.7'
    }

    test {
        useJUnitPlatform()
    }

    jacoco {
        toolVersion = '0.8.1'
    }

    jacocoTestReport {
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }

    kotlin {
        experimental {
            coroutines "enable"
        }
    }

    test.finalizedBy jacocoTestReport

    task quickTestRun(type: JavaExec) {
        classpath sourceSets.main.runtimeClasspath
        main = "org.tenkiv.daqc.TestKt"
    }
}

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}